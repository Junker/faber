#!/usr/bin/env gosh

(use scheme.list)
(use srfi-13)
(use scheme.show)
(use scheme.show.color)
(use gauche.process)
(use gauche.parseopt)
(use file.util)

(define *task-table* (make-hash-table 'eq?))
(define *default-file-name* "faberfile")
(define *verbose* #f)

(define-macro (define-task name . rest)
  `(hash-table-put! *task-table*
                    (quote ,name) (lambda ,(car rest) ,@(cdr rest))))

(define-macro (task name . rest)
  `(define-task ,name ,@rest))

(define (%run-task name args)
  (when *verbose*
    (show #t (as-green (format #f "Running task ~S..." name)) nl))
  (let1 taskfun (hash-table-ref *task-table* name
                                (lambda ()
                                  (exit-with-error
                                   (format #f "Task '~S' doesn't exist" name))))
    (apply taskfun args)))

(define-macro (run-task name . args)
  `(%run-task (quote ,name) (list ,@args)))

(define (print-verbose-command command)
  (when *verbose*
    (show #t (as-bold (format #f "~A" command)) nl)))

(define (run command . args)
  (print-verbose-command command)
  (apply do-process! command args))

(define (run-string command . args)
  (print-verbose-command command)
  (apply process-output->string command args))

(define (run-lines command . args)
  (print-verbose-command command)
  (apply process-output->string-list command args))

(define (run-pipe commands . args)
  (print-verbose-command (string-join (map (lambda (cmd) (format #f "~A" cmd))
                                           commands)
                                      " | "))
  (apply do-pipeline commands args))

(define (sh command)
  (print-verbose-command command)
  (sys-system command))

(define-macro (run* args)
  `(run (list ,@args)))

(define-macro (run-string* args)
  `(run-string (list ,@args)))

(define-macro (run-lines* args)
  `(run-lines (list ,@args)))

(define (process-output->file command filename . opts)
  (call-with-output-file filename
    (lambda (in)
      (apply call-with-input-process command
             (lambda (out)
               (copy-port out in))
             opts))))

(define (run/file command . args)
  (print-verbose-command command)
  (apply process-output->file command args))

(define (print-tasks)
  (show #t "Available tasks:" nl)
  (let1 tasks (map symbol->string (hash-table-keys *task-table*))
    (dolist (task (sort tasks string<?))
      (show #t (as-green task) nl))))

(define (print-help)
  (show #t (as-yellow "Usage: ") (as-green "faber ")
        "[OPTIONS] [TASK] [ARGUMENTS]" nl nl)
  (show #t (as-yellow "Options:") nl)
  (for-each (lambda (pair)
              (show #t (as-green "  " (fitted/right 34 (car pair)))
                    (cdr pair) nl))
            '(("-f, --faberfile <FABERFILE>" . "Use <FABERFILE> as justfile")
              ("-l, --list" . "List available tasks")
              ("-v, --verbose" . "Use verbose output"))))

(define (exit-with-error err :optional code)
  (show #t (as-red err))
  (exit (or code 1)))

(define (load-faber-file file)
  (cond ((not (file-exists? file))
         (exit-with-error (format #f "File ~S doesn't exist" file)))
        ((not (file-is-readable? file))
         (exit-with-error (format #f "File ~S is unreadable" file)))
        (#t
         (load file))))

(define (main args)
  (let-args (cdr args)
    ((%help "h|help")
     (%list "l|list")
     (%file "f|faberfile=s" *default-file-name*)
     (%workdir "d|working-directory=s")
     (%verbose "v|verbose" #f)
     . restargs)
    ;;
    (when (relative-path? %file)
      (set! %file (build-path (current-directory)
                              %file)))
    (current-directory (or %workdir
                           (decompose-path (expand-path %file))))
    (if %help
        (begin
          (print-help)
          (exit))
        (load-faber-file %file))
    (cond ((null? restargs)
           (if (hash-table-exists? *task-table* 'default)
               (run-task default)
               (print-tasks)))
          (%list
           (print-tasks))
          ((not (null? restargs))
           (begin
             (set! *verbose* %verbose)
             (%run-task (string->symbol (car restargs))
                        (cdr restargs))))))
  0)
