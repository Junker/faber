#!/usr/bin/env gosh

(use scheme.list)
(use srfi-13)
(use scheme.show)
(use scheme.show.color)
(use gauche.process)
(use gauche.parseopt)
(use file.util)
(use control.pmap)

(define *task-table* (make-hash-table 'eq?))
(define *default-file-name* "faberfile")
(define *verbose* #f)

(define-macro (define-task name . rest)
  `(hash-table-put! *task-table*
                    (quote ,name) (lambda ,(car rest) ,@(cdr rest))))

(define-macro (deftask name . rest)
  `(define-task ,name ,@rest))

(define (%run-task name args)
  (when *verbose*
    (show #t (as-green (format #f "Running task '~S'..." name)) nl))
  (let1 taskfun (hash-table-ref *task-table* name
                                (lambda ()
                                  (exit-with-error
                                   (format #f "Task '~S' doesn't exist" name))))
    (apply taskfun args)))

(define-macro (run-task name . args)
  `(%run-task (quote ,name) (list ,@args)))

(define (print-verbose-command command)
  (when *verbose*
    (show #t (as-bold (x->string command)) nl)))

(define (run command :key (noerr? #f) (quiet? #f))
  (print-verbose-command command)
  (do-process command
              :on-abnormal-exit (if noerr? #f :error)
              :output (if quiet? :null #f)))

(define (run->string command :key (noerr? #f))
  (print-verbose-command command)
  (process-output->string command
                          :on-abnormal-exit (if noerr? #f :error)))

(define (run->lines command :key (noerr? #f))
  (print-verbose-command command)
  (process-output->string-list command
                               :on-abnormal-exit (if noerr? #f :error)))

(define (run-pipe commands :key (noerr? #f) (quiet? #f))
  (print-verbose-command (string-join (map x->string commands)
                                      " | "))
  (do-pipeline commands
               :on-abnormal-exit (if noerr? #f :error)
               :output (if quiet? :null #f)))

(define (run-parallel commands :key (num-threads #f) (noerr? #f) (quiet? #f))
  (pmap run commands :mapper (if num-threads
                                 (make-static-mapper num-threads)
                                 (make-fully-concurrent-mapper))))

(define (sh command :key (noerr? #f))
  (print-verbose-command command)
  (let1 errcode (sys-system command)
    (if (and (not noerr?)
             (positive? errcode))
        (error (format #f "process exited abnormally with exit code ~A"
                       errcode)))))

(define-macro (run* . args)
  `(run (list ,@args)))

(define-macro (run->string* . args)
  `(run->string (list ,@args)))

(define-macro (run->lines* . args)
  `(run->lines (list ,@args)))

(define (process-output->file command filename . opts)
  (call-with-output-file filename
    (lambda (in)
      (apply call-with-input-process command
             (lambda (out)
               (copy-port out in))
             opts))))

(define (run->file command filename :key (noerr? #f))
  (print-verbose-command command)
  (process-output->file command filename
                        :on-abnormal-exit (if noerr? #f :error)))

(define (print-tasks)
  (show #t "Available tasks:" nl)
  (let1 tasks (hash-table-map *task-table*
                              (lambda (name proc)
                                (cons (symbol->string name)
                                      (cdr (procedure-info proc)))))
    (dolist (task (sort tasks string<? car))
      (show #t (as-bold (as-green (car task))) " " (as-yellow (cdr task)) nl))))

(define (print-summary)
  (show #t (joined displayed (hash-table-keys *task-table*) " ")))

(define (print-help)
  (show #t (as-yellow "Usage: ") (as-green "faber ")
        "[OPTIONS] [TASK] [ARGUMENTS]" nl nl)
  (show #t (as-yellow "Options:") nl)
  (for-each (lambda (pair)
              (show #t (as-green "  " (fitted/right 40 (car pair)))
                    (cdr pair) nl))
            '(("-f, --faberfile <FABERFILE>" . "Use <FABERFILE> as faberfile")
              ("-l, --list" . "List available tasks")
              ("-d, --working-directory <WORKING-DIR>" . "Use <WORKING-DIR> as working directory")
              ("-v, --verbose" . "Use verbose output"))))

(define (exit-with-error err :optional code)
  (show #t (as-red err))
  (exit (or code 1)))

(define (load-faber-file file)
  (cond ((not (file-exists? file))
         (exit-with-error (format #f "File ~S doesn't exist" file)))
        ((not (file-is-readable? file))
         (exit-with-error (format #f "File ~S is unreadable" file)))
        (#t
         (load file))))

(define (main args)
  (let-args (cdr args)
    ((%help "h|help")
     (%list "l|list")
	   (%summary "s|summary")
     (%file "f|faberfile=s" *default-file-name*)
     (%workdir "d|working-directory=s")
     (%verbose "v|verbose" #f)
     . restargs)
    ;;
    (set! *verbose* %verbose)
    (when %help
      (begin
        (print-help)
        (exit)))
    (let1 absfile (if (absolute-path? %file)
                      %file
                      (build-path (current-directory)
                                  %file))
      (current-directory (or %workdir
                             (decompose-path (expand-path absfile))))
      (load-faber-file absfile))
    (cond (%list
           (print-tasks))
          (%summary
           (print-summary))
          ((null? restargs)
           (if (hash-table-exists? *task-table* 'default)
               (run-task default)
               (print-tasks)))
          (else
           (%run-task (string->symbol (car restargs))
                      (map (lambda (arg)
                             (if (string-prefix? ":" arg)
                                 (make-keyword (string-drop arg 1))
                                 arg))
                           (cdr restargs))))))
  0)
